---
docs/7-infrastructure-configuration-management/7.1.3-terraform-modules.md:
  category: Infrastructure as Code
  estReadingMinutes: 15
  exercises:
    -
      name: Registry Modules
      description: Learn how to use registry modules to create an EC2 instance and VPC in AWS, or add an EC2 instance to an existing VPC.
      estMinutes: 90
      technologies:
      - Terraform
      - AWS EC2
      - AWS VPC
    -
      name: Custom Modules/Local Modules
      description: Create and use a local module for an S3 bucket in AWS.
      estMinutes: 75
      technologies:
      - Terraform
      - AWS S3
---

# Terraform Modules

The previous sections should have illustrated how flexible and powerful Terraform is. As organizations adopt Terraform for more of their infrastructure and size of their Terraform configurations grow they also need better ways to organize and maintain those configurations. Terraform configurations can be organized into modules to make the code more reusable and easier to maintain. In fact any Terraform code in a folder is a module so you have already written Terraform modules and not known it! Of course what makes Terraform modules useful is the ability to use them together and share them with others.

Read HashiCorp's [Terraform module overview](https://developer.hashicorp.com/terraform/tutorials/modules/module) to learn more about Terraform modules features and uses.

 As mentioned, there are registry and remote modules. Both types of modules are below. By this point throughout the Terraform chapters, you may or may not have a VPC in the AWS account you are deploying to. The first exercise creates an ec2 instance and VPC in AWS. Alternatively you may want to add an EC2 instance to an existing VPC, that is also covered below.

## Exercise

### **Registry Modules**

Before beginning this exercise, modify your terraform "required_version" to "1.8.2" (latest) and your aws provider version to "5.48.0"

```hcl
terraform {
    required_providers {
      aws = {
        source  = "hashicorp/aws"
        version = "5.48.0"
      }
    }

    backend "s3" {
      bucket = "<your-bucket-name>"
      key    = "<bucket-key>"
      region = "<region>"
    }

  required_version = "1.8.2"
}
```

There are a multitude of registry modules available. For this exercise the modules used are managed by HashiCorp.

?> Whenever you add a new module to a configuration, Terraform must install the module before it can be used. Both the `terraform get` and `terraform init` commands will install and update modules.

1. Create a `variables.tf` file and add the following variables. If you already have a `variables.tf` file, add them there. Note that there are several variables that should be updated as they should be specific to your stack, variables such as `vpc_name` should be updated. Update the `default` parameter

```hcl
variable "vpc_name" {
  description = "Name of VPC"
  type        = string
  default     = "<your-vpc-name>"
}

variable "vpc_cidr" {
  description = "CIDR block for VPC"
  type        = string
  default     = "10.0.0.0/16"
}

variable "vpc_azs" {
  description = "Availability zones for VPC"
  type        = list(string)
  default     = ["us-east-2a", "us-east-2b", "us-east-2c"]
}

variable "vpc_private_subnets" {
  description = "Private subnets for VPC"
  type        = list(string)
  default     = ["10.0.1.0/24", "10.0.2.0/24"]
}

variable "vpc_public_subnets" {
  description = "Public subnets for VPC"
  type        = list(string)
  default     = ["10.0.101.0/24", "10.0.102.0/24"]
}

variable "vpc_enable_nat_gateway" {
  description = "Enable NAT gateway for VPC"
  type        = bool
  default     = true
}

variable "vpc_tags" {
  description = "Tags to apply to resources created by VPC module"
  type        = map(string)
  default = {
    Terraform   = "true"
    Environment = "sandbox"
  }
}
```

2. Review the configuration below first. The `  source  = "terraform-aws-modules/vpc/aws"
` and `  source  = "terraform-aws-modules/ec2-instance/aws"
` declare the registry and module that's being used for the modules in this configuration. In addition, familiarize yourself with both of these modules by reviewing the documentation for both.

* [HashiCorp AWS VPC Module](https://registry.terraform.io/modules/terraform-aws-modules/vpc/aws/latest)
* [HashiCorp AWS EC2-instance Module](https://registry.terraform.io/modules/terraform-aws-modules/ec2-instance/aws/latest)

3. Create a new `.tf` file or add the following configuration to your `main.tf` file from previous sections of the boot camp.

4. For this exercise the `ami` id is hardcoded. A new ID can be found in the AWS console or the AWS CLI. This AWS user guide walks through that process - [Find a Linux AMI](https://docs.aws.amazon.com/AWSEC2/latest/UserGuide/finding-an-ami.html#finding-an-ami-console)

?> As mentioned previously, the configuration below creates a new VPC for the EC2 instance to reside in. If you don't want to create a new VPC, you can omit the VPC module from your configuration. For this exercise, the ec2 instance relies on outputs from the VPC module. If you omit the VPC module, `vpc_security_group_ids` and `subnet_id` will have to be hardcoded based on resources available in the AWS account you're deploying into.

```hcl
module "vpc" {
  source  = "terraform-aws-modules/vpc/aws"
  version = "5.8.1"

  name = var.vpc_name
  cidr = var.vpc_cidr

  azs             = var.vpc_azs
  private_subnets = var.vpc_private_subnets
  public_subnets  = var.vpc_public_subnets

  enable_nat_gateway = var.vpc_enable_nat_gateway

  tags = var.vpc_tags
}

module "ec2_instance" {
  source  = "terraform-aws-modules/ec2-instance/aws"
  version = "5.6.1"

  count = 1
  name  = "my-ec2-cluster-${count.index}"

  ami                    = "ami-0a1179631ec8933d7" # Amazon Linux 2
  instance_type          = "t2.micro"
  vpc_security_group_ids = [module.vpc.default_security_group_id]
  subnet_id              = module.vpc.private_subnets[0]

  tags = {
    Terraform   = "true"
    Environment = "sandbox"
  }
}
```

3. Commit and push these changes to your repository. Push this new configuration to a branch that is configured to run a `terraform plan.` View the results of the plan before proceeding to deploy.

<br>
<br>

### **Custom Modules/Local Modules**

1. Review the configuration first. The ` source = "./modules/s3-bucket"`  declares the local module directory and module that's being used for the modules in this configuration.

2. Create a new `.tf` file or add the following configuration to your `main.tf` file from previous sections of the boot camp. This is declaring the module that we will use. The next steps we lay out the steps for creating this local module.

?> Replace the `bucket_name` with an override to the local modules default

```hcl
module "website_s3_bucket" {
  source = "./modules/s3-bucket"

  bucket_name = "<bucket-override-name>"
}
```

3. Create a `modules` directory. Inside the modules directory create a `s3-bucket` directory. The directory path should be `/modules/s3-bucket`
 with  the following files and content.

* `main.tf`
* `variables.tf`
* `outputs.tf`

#### *main.tf*

```hcl
resource "aws_s3_bucket" "s3_bucket" {
  bucket = var.bucket_name
  tags = var.tags
}

resource "aws_s3_bucket_ownership_controls" "s3_bucket" {
  bucket = aws_s3_bucket.s3_bucket.id
  rule {
    object_ownership = "BucketOwnerPreferred"
  }
}

resource "aws_s3_bucket_acl" "s3_bucket" {
  depends_on = [ aws_s3_bucket_ownership_controls.s3_bucket ]
  bucket = aws_s3_bucket.s3_bucket.id
  acl = "private"
}
```

#### *variables.tf*

?> Note that there these variables  should be updated as they are  specific to your stack. Update the `default` parameter for each.

```hcl
variable "bucket_name" {
  description = "Name of the s3 bucket. Must be unique."
  type        = string
  default = "<your bucket name>"
}

variable "tags" {
  description = "Tags to set on the bucket."
  type        = map(string)
  default = {
    Terraform   = "true"
    Environment = "sandbox"
  }
}

```

#### *outputs.tf*

```hcl
output "arn" {
  description = "ARN of the bucket"
  value       = aws_s3_bucket.s3_bucket.arn
}

output "name" {
  description = "Name (id) of the bucket"
  value       = aws_s3_bucket.s3_bucket.id
}

```

4. Commit and push these changes to your repository. Push this new configuration to a branch that is configured to run a `terraform plan.` View the results of the plan before proceeding to deploy.
