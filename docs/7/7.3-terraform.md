# Terraform

Terraform is an [open source](https://github.com/hashicorp/terraform) tool by Hashicorp for building, updating, and composing infrastructure using a declarative configuration language. This is a pretty loaded sentence, so let's break it down term by term.

### Infrastructure

In the context of Terraform, infrastructure may refer to:

- Cloud compute resources (ex. EC2, Google Compute Instance)
- Cloud network configuration (ex. VPC, Google Compute Network)
- DNS configuration (ex. Route53, CloudFlare)
- ...many more

As you can see from this list, Terraform has a heavy focus on cloud platforms. This list is definitely not exhaustive, Terraform supports a [huge number of providers.](https://www.terraform.io/docs/providers/index.html)

### Declarative Configuration Language (HCL)

Terraform's configuration syntax gives you a way to "describe" the infrastructure that you want. Here's how you would describe an EC2 instance:

```hcl
resource "aws_instance" "my_instance" {
  ami           = "ami-01d025118d8e760db" # Amazon linux
  instance_type = "t2.micro"
  key_name      = "eddieb"

  tags = {
    Name = "HelloWorld"
  }
}
```
This declares a single AWS EC2 instance resource with a specific machine image id, instance type, key pair name, and Name tag.

### Building

Internally, Terraform transforms your resource descriptions into a sequence calls to existing APIs. For example, Terraform uses the [AWS SDK for Go](https://docs.aws.amazon.com/sdk-for-go/api/) to provision resources on the AWS platform.

### Updating

After provisioning your described infrastructure, Terraform stores the current state of your configuration as well as metadata returned from the API calls (Like EC2 Instance Ids). When you modify configuration, Terraform uses this previously stored data to determine what changes to make to bring the infrastructure up to date with the new configuration.

### Composing

Resources often need to be linked together in some fashion to function. For instance, to apply security rules to an EC2 instance, you need to attach a security group to it. Terraform enables these kinds of compositions by allowing you to reference resources' attributes.

```hcl
resource "aws_security_group" "ssh" {
  name        = "ssh_access"
  description = "Allow ssh traffic"

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "ssh_access"
  }
}


resource "aws_instance" "my_instance" {
  # ...
  vpc_security_group_ids = ["${aws_security_group.ssh.id}"]
  # ...
}
```

This creates an inherent resource dependency from the EC2 instance to the Security Group. Terraform is capable of recognizing this dependency, so when the Security Group is created or updated, the EC2 instance will be updated as well.

## Exercise 1 - Getting Started

**Modify the above configuration to create your own EC2 instance with a Security Group.**

1. Install Terraform using HomeBrew.
2. Create a file _my-instance.tf_ with the following contents:

  ```hcl
  provider "aws" {
    region = "us-east-1"
  }
  ```

3. Copy the EC2 Instance and Security Group resources from above into _my-instance.tf_.
4. Substitute your own key for the key_name attribute. If multiple people are working on this exercise, you should each choose a unique record name as well.
5. Run `terraform plan` to see a manifest of the resources being created, then run `terraform apply` to provision your resources
6. Run `terraform show` and look for the public ip of your instance. Try shelling in.
7. Running `terraform apply` produced a file _terraform.tfstate_. Open this file and see what sort of data Terraform stores between runs (but don't modify it!).
8. Update the Name tag of your instance in the configuration file. Run `terraform plan` and `terraform apply`. What did Terraform do?
9. Update the _keypair_ of the instance to a different key pair. Once again run `terraform plan` and `terraform apply`. What did Terraform do this time?
10. Through the AWS console change the Name tag of your instance to something else, then run `terraform plan` and `terraform apply`. What happened?
11. Add a new security group to allow http and https access and attach it to your instance. Again run `terraform plan` and `terraform apply`. How did this affect the preexisting resources?
12. When you are done examining things, run `terraform destroy` to get rid of all your resources.

## Resources

In Terraform, a resource is a single piece of infrastructure to be provisioned. You've already seen a couple examples of resources above - _aws.instance_ and _aws.security.group_. Behind the scenes, a resource is basically an encapsulation of API call(s) with some input and output values. Each resource has a set of parameters that need to be defined as well as a set of output attributes that get populated once the resource is finished provisioning. Here are some of the parameters and output attributes for _aws.instance_:

**Parameters**
- ami
- availability_zone
- instance_type
- key_name
- security_groups
- tags
- user_data

**Output Attributes**
- id
- public_dns
- public_ip
- private_dns
- private_ip
- security_groups
- availability_zone

As mentioned earlier, if a resource **x** uses an output attribute of another resource **y**, then **x** depends on **y**. This means that when **y** is updated and its output attributes change, **x** must also be updated. This also has an effect on the order of execution when applying a configuration. Resources that don't depend on each other can be provisioned simultaneously, while resources with dependencies must be blocked until the resource they depend on is provisioned.

Resources are namespaced under _Providers_. All the resources we've seen thus far have been from the AWS provider, but there are a lot of [other providers](https://www.terraform.io/docs/providers/index.html) for various technologies and platforms.

> ### Recommended Reading
>#### Before continuing, I suggest you skim through the [ Resource Configuration Docs](https://www.terraform.io/docs/configuration/resources.html) to gain a more complete understanding of how resources work.

## Variables

Variables allow you to parameterize Terraform configuration. Variables are useful for cases where you want to reuse a Terraform configuration with some slight adjustments to fit the given situation. Say you want to have a Jenkins master instance in both a staging and production deploy environments. The resource requirements for the staging cluster will probably be much lighter than production, so it would make sense to use a lighter instance type for staging to cut costs. To accomplish this, you could parameterize the instance type as a Variable:

```hcl
variable "jenkins_instance_type" {
  type = "string"
}

resource "aws_instance" "jenkins" {
  ami           = "ami-4836a428" # Amazon linux
  instance_type = "${var.jenkins_instance_type}"
  key_name      = "eddieb"

  tags = {
    Name = "HelloWorld"
  }
}
```
Now when you run `terraform plan` or apply, you will be prompted to enter values for the variables you have defined.

```
$ terraform plan
var.jenkins_instance_type
  Enter a value:
```
You can also store parameter values in a file that can be passed to Terraform. This is useful for when you save a group of parameter values for reuse. You might create a file _staging.tfvars_ with the following content:
```
jenkins_instance_type = "t2.micro"
```

Now you can load the Jenkins configuration with the staging parameter values:

```
$ terraform plan -var-file=staging.tfvars
```

## Data Sources

There's a lot of situations where you need to fetch and parse some remote data. Data Sources allow you to do just that. For example, you can pull the attributes of an elastic ip to attach it to your instance.

```hcl
variable "elastic_ip" {}


resource "aws_instance" "nginx" {
  ami           = "ami-4836a428" # Amazon linux
  instance_type = "t2.micro"
  key_name      = "eddieb"

  tags = {
    Name = "nginx"
  }
}


data "aws_eip" "proxy_ip" {
  public_ip = "${var.elastic_ip}"
}

resource "aws_eip_association" "proxy_eip" {
  instance_id   = "${aws_instance.nginx.id}"
  allocation_id = "${data.aws_eip.proxy_ip.id}"
}
```
As an entity, a data source behaves similarly to a resource. It has parameters and output attributes that can be referenced. When a data source is updated, anything that uses its output attributes must also be updated. The key difference is that with a data source, nothing is being created (only fetched).

## Interpolation Syntax

We've been talking about referencing variables and attributes, but we haven't explicitly stated how that's done. Any value can be referenced using Terraform's Interpolation syntax. Depending on where the value comes from, we can reference it in one of several ways:

```hcl
# Resource Attributes
val     = "${<resource>.<name>.<attribute>}"
example = "${aws_instance.jenkins.public_ip}"


# Variables
val     = "${var.<variable_name>}"
example = "${var.jenkins_instance_type}"


# Data Source attributes
val     = "${data.<data_source>.<name>.<attribute>}"
example = "${data.aws_route53_zone.liatrio.zone_id}"
```
The interpolation syntax allows you to combine values and string literals however you want:

```hcl
jenkins_url = "http://${aws_instance.jenkins.public_ip}:8080"
```
You can also use Terraform's [built-in functions](https://www.terraform.io/docs/configuration/interpolation.html#built-in-functions), like _file_:
```hcl
private_key = "${file("~/.ssh/id_rsa")}"
```

>#### Recommended Reading: Before continuing, I suggest you skim through the [Interpolation Syntax Docs](https://www.terraform.io/docs/configuration/interpolation.html#file-path-) to gain a more complete understanding of how Terraform's interpolation works.

## Further Reading

Skim through the [Terraform Configuration](https://www.terraform.io/docs/configuration/index.html) docs and [CLI docs](https://www.terraform.io/docs/commands/index.html). Take note of any interesting features or behaviors that weren't covered in this section.

<center>

## Deliverables

</center>

<div class="grid2"><div class="col">
<center>

**Engineers**

</center>

- What do you think of Terraform? What do you like/dislike about it?
- How do you imagine Terraform being used to provision production infrastructure?
- How might you integrate Terraform into a CI system?

</div><div class="col">
<center>

**Consultants**

</center>

- What is this tool?
- How is it used in an enterprise?
- Licensing costs
- Liatrio's POV/uses for Terraform
- Explain pros and cons
- Create high level deck explaining Terraform. Coordinate with your team to also have a technical demo
- Once an engineer has completed this exercise work with them to set up a server using Terraform

</div></div>
