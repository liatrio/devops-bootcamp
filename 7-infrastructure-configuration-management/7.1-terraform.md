# Terraform

Terraform is an [open source](https://github.com/hashicorp/terraform) tool by Hashicorp for **building**, **updating**, and **composing** **infrastructure** using a **declarative configuration language**. This is a pretty loaded sentence, so let's break it down term by term.

?> Hashicorp also offers hosted cloud and enterprise Terraform solutions

### Infrastructure

In the context of Terraform, infrastructure may refer to:

- Cloud compute resources (ex. EC2, Google Compute Instance)
- Cloud network configuration (ex. VPC, Google Compute Network)
- DNS configuration (ex. Route53, CloudFlare)
- ...many more

As you can see from this list, Terraform has a heavy focus on cloud platforms. This list is definitely not exhaustive, Terraform supports a [huge number of providers.](https://www.terraform.io/docs/providers/index.html)

### Declarative Configuration Language (HCL)

Terraform's configuration syntax gives you a way to "describe" the infrastructure that you want. Here's how you would describe an EC2 instance:

```hcl
resource "aws_instance" "my_instance" {
  ami           = "ami-01d025118d8e760db" # Amazon linux
  instance_type = "t2.micro"
  key_name      = "eddieb"

  tags = {
    Name = "HelloWorld"
  }
}
```

This declares a single AWS EC2 instance resource with a specific machine image id, instance type, key pair name, and Name tag.

### Building

Internally, Terraform transforms your resource descriptions into a sequence calls to existing APIs. For example, Terraform uses the [AWS SDK for Go](https://docs.aws.amazon.com/sdk-for-go/api/) to provision resources on the AWS platform.

### Updating

After provisioning your described infrastructure, Terraform stores the current state of your configuration as well as metadata returned from the API calls (Like EC2 Instance Ids). When you modify configuration, Terraform uses this previously stored data to determine what changes to make to bring the infrastructure up to date with the new configuration.

### Composing

Resources often need information about other resources to be provisioned. For instance, to apply security rules to an EC2 instance, you need to attach a security group to it. Terraform enables these kinds of compositions by allowing you to reference resources' attributes.

```hcl
resource "aws_security_group" "ssh" {
  name        = "ssh_access"
  description = "Allow ssh traffic"

  ingress {
    from_port   = 22
    to_port     = 22
    protocol    = "tcp"
    cidr_blocks = ["0.0.0.0/0"]
  }

  egress {
    from_port   = 0
    to_port     = 0
    protocol    = "-1"
    cidr_blocks = ["0.0.0.0/0"]
  }

  tags = {
    Name = "ssh_access"
  }
}


resource "aws_instance" "my_instance" {
  # ...
  vpc_security_group_ids = [aws_security_group.ssh.id]
  # ...
}
```

This creates an inherent resource dependency from the EC2 instance to the Security Group. Terraform is capable of recognizing this dependency, so when the Security Group is created or updated, the EC2 instance will be updated as well.

## Resources

In Terraform, a resource is a single piece of infrastructure to be provisioned. You've already seen a couple examples of resources above - _aws.instance_ and _aws.security.group_. Behind the scenes, a resource is basically an encapsulation of API call(s) with some input and output values. Each resource has a set of parameters that need to be defined as well as a set of output attributes that get populated once the resource is finished provisioning. Here are some of the parameters and output attributes for _aws.instance_:

**Parameters**

- AMI
- availability_zone
- instance_type
- key_name
- security_groups
- tags
- user_data

**Output Attributes**

- id
- public_dns
- public_ip
- private_dns
- private_ip
- security_groups
- availability_zone

As mentioned earlier, if a resource **x** uses an output attribute of another resource **y**, then **x** depends on **y**. This means that when **y** is updated and its output attributes change, **x** must also be updated. This also has an effect on the order of execution when applying a configuration. Resources that don't depend on each other can be provisioned simultaneously, while resources with dependencies must be blocked until the resource they depend on is provisioned.

Resources are namespaced under _Providers_. All the resources we've seen thus far have been from the AWS provider, but there are a lot of [other providers](https://www.terraform.io/docs/providers/index.html) for various technologies and platforms.

?> Before continuing, you should skim through the [Resource Configuration Docs](https://www.terraform.io/docs/configuration/resources.html) to gain a more complete understanding of how resources work.

## Variables

Variables allow you to parameterize Terraform configuration. Variables are useful for cases where you want to reuse a Terraform configuration with some slight adjustments to fit the given situation. Say you want to have a Jenkins master instance in both a staging and production deploy environments. The resource requirements for the staging cluster will probably be much lighter than production, so it would make sense to use a lighter instance type for staging to cut costs. To accomplish this, you could parameterize the instance type as a Variable:

```hcl
variable "jenkins_instance_type" {
  type = "string"
}

resource "aws_instance" "jenkins" {
  ami           = "ami-4836a428" # Amazon linux
  instance_type = var.jenkins_instance_type
  key_name      = "eddieb"

  tags = {
    Name = "HelloWorld"
  }
}
```

Now when you run `terraform plan` or apply, you will be prompted to enter values for the variables you have defined.

```bash
$ terraform plan
var.jenkins_instance_type
  Enter a value:
```

You can also store parameter values in a file that can be passed to Terraform. This is useful for when you save a group of parameter values for reuse. You might create a file _staging.tfvars_ with the following content:

```bash
jenkins_instance_type = "t2.micro"
```

Now you can load the Jenkins configuration with the staging parameter values:

```bash
terraform plan -var-file=staging.tfvars
```

## Data Sources

There's a lot of situations where you need to fetch and parse some remote data. Data Sources allow you to do just that. For example, you can pull the attributes of an elastic ip to attach it to your instance.

```hcl
variable "elastic_ip" {}


resource "aws_instance" "nginx" {
  ami           = "ami-4836a428" # Amazon linux
  instance_type = "t2.micro"
  key_name      = "eddieb"

  tags = {
    Name = "nginx"
  }
}


data "aws_eip" "proxy_ip" {
  public_ip = var.elastic_ip
}

resource "aws_eip_association" "proxy_eip" {
  instance_id   = aws_instance.nginx.id
  allocation_id = data.aws_eip.proxy_ip.id
}
```

As an entity, a data source behaves similarly to a resource. It has parameters and output attributes that can be referenced. When a data source is updated, anything that uses its output attributes must also be updated. The key difference is that with a data source, nothing is being created (only fetched).

## Expressions

We've been talking about referencing variables and attributes, but we haven't explicitly stated how that's done. Any value can be referenced using Terraform's Expression syntax. Depending on where the value comes from, we can reference it in one of several ways:

```hcl
# Resource Attributes
val     = <resource>.<name>.<attribute>
example = aws_instance.jenkins.public_ip


# Variables
val     = var.<variable_name>
example = var.jenkins_instance_type


# Data Source attributes
val     = data.<data_source>.<name>.<attribute>
example = data.aws_route53_zone.liatrio.zone_id
```

You can also combine string literals with variable values using [interpolation](https://www.terraform.io/docs/language/expressions/strings.html#interpolation).

```hcl
jenkins_url = "http://${aws_instance.jenkins.public_ip}:8080"
```

Terraform also has [built-in functions](https://www.terraform.io/docs/language/functions/index.html) which can be used to modify expression values.

```hcl
private_key = "${file("~/.ssh/id_rsa")}"
```

## Further Reading

Look over the following Terraform documentation get an better idea of features not covered in this section.

- [Terraform Configuration Language](https://www.terraform.io/docs/language/index.html)
- [Terraform CLI](https://www.terraform.io/docs/cli/index.html)

<center>

## Deliverables

</center>

<div class="grid2"><div class="col">
<center>

**Engineers**

</center>

- What do you think of Terraform? What do you like/dislike about it?
- How do you imagine Terraform being used to provision production infrastructure?
- How might you integrate Terraform into a CI system?

</div><div class="col">
<center>

**Consultants**

</center>

- What is this tool?
- How is it used in an enterprise?
- Licensing costs
- Liatrio's POV/uses for Terraform
- Explain pros and cons
- Create high level deck explaining Terraform. Coordinate with your team to also have a technical demo
- Once an engineer has completed this exercise work with them to set up a server using Terraform

</div></div>
